// default package

terminals S Z

syntax
   n ::= Z 
      | S n
   
   0 := Z
   1 := S Z
   2 := S S Z
   3 := S 2
   4 := S 3
       
judgment plus: n1 + n2 = n3

   ---------- plus-zero
   0 + n = n
   
   n1 + n2 = n3
   ----------------- plus-succ
   S n1 + n2 = S n3
   
lemma easy: 
    exists 1 + 1 = 2
    p: 0 + 1 = 1 by rule plus-zero
    proof by rule plus-succ on p
end lemma

lemma next:
    exists 2 + 2 = 4
    p1: 0 + 2 = 2 by rule plus-zero
    p2: 1 + 2 = 3 by rule plus-succ on p1
    p3: 2 + 2 = 4 by rule plus-succ on p2     
end lemma   

theorem plus-right-zero:
    forall n
    exists n + 0 = n
    use induction on n
    // circular: proof by theorem plus-right-zero on n
    proof by case analysis on n:
        case Z is
            proof by rule plus-zero
        end case
        case S n' is
            p': n' + 0 = n' by induction hypothesis on n'
            p: n' + 0 = n' by theorem plus-right-zero on n' // induction hypothesis
            proof by rule plus-succ on p
        end case
    end case analysis
end theorem

lemma plus-right-succ:
    forall p: n1 + n2 = n3
    exists n1 + S n2 = S n3
    use induction on p
    proof by case analysis on p:
        case rule
            ------------- plus-zero
            _: 0 + n2 = n2
            where n1 := 0
            and n3 := n2
        is
            proof by rule plus-zero
        end case
        case rule 
            p': n1' + n2 = n3'
            ------------------------- plus-succ
            _: S n1' + n2 = S n3'
            where n1 := S n1'
            and n3 := S n3'
        is
            q': n1' + S n2 = S n3' by induction hypothesis on p'
            proof by rule plus-succ on q'
        end case
    end case analysis
end lemma

theorem plus-commutative:
    forall p: n1 + n2 = n3
    exists n2 + n1 = n3
    proof by induction on p:
        case rule
            --------------- plus-zero
            _: 0 + n2 = n2
            where n1 := Z
             and  n3 := n2
        is
            proof by theorem plus-right-zero on n2
        end case
        case rule
            p': n1' + n2 = n3'
            ---------------------- plus-succ
            _: S n1' + n2 = S n3'
            where n1 := S n1'
            and n3 := S n3'        
        is
            q': n2 + n1' = n3' by induction hypothesis on p'
            proof by lemma plus-right-succ on q'
        end case
    end induction
end theorem

theorem plus-total:
    forall n1 
    forall n2
    exists n1 + n2 = n3
    proof by induction on n1:
        case Z is
            proof by rule plus-zero
        end case
        case S n1' is
            p': n1' + n2 = n3' by induction hypothesis on n1', n2
            proof by rule plus-succ on p'
        end case
    end induction
end theorem

judgment equal: n = n

    ----- eq
    n = n
    
theorem plus-unique:
    forall p3: n1 + n2 = n3
    forall p4: n1 + n2 = n4
    exists n3 = n4
    proof by case analysis on p3:
        case rule
            ----------------- plus-zero
            _: 0 + n2 = n2
            where n1 := 0
            and n3 := n2
        is
            proof by case analysis on p4:
                case rule
                    ------------ plus-zero
                    _: 0 + n2 = n2
                    where n4 := n2
                is
                    proof by rule eq
                end case
            end case analysis
        end case
    end case analysis
end theorem